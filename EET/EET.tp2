BACKUP ~EET/backup~
AUTHOR ~K4thos (swit)~
VERSION ~1.0 RC7~
README ~EET/lang/%LANGUAGE%/readme-EET.html~ ~EET/readme-EET.html~

LANGUAGE ~English~
	~en_US~
	~EET/lang/en_US/prompts.tra~
	~EET/lang/en_US/setup.tra~
LANGUAGE ~Polski (Polish)~
	~pl_PL~
	~EET/lang/pl_PL/prompts.tra~
	~EET/lang/pl_PL/setup.tra~
LANGUAGE ~Spanish~
	~es_ES~
	~EET/lang/es_ES/prompts.tra~
	~EET/lang/es_ES/setup.tra~
/*LANGUAGE ~Deutsch (German)~
	~de_DE~
	~EET/lang/de_DE/prompts.tra~
	~EET/lang/de_DE/setup.tra~
LANGUAGE ~French~
	~fr_FR~
	~EET/lang/fr_FR/prompts.tra~
	~EET/lang/fr_FR/setup.tra~*/

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Initialise EET (first mod in install order)      \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @900000 //Initialise EET (first mod in install order)
REQUIRE_PREDICATE ENGINE_IS ~bg2ee~ @900001 //Please install it on BG2:EE engine
FORBID_COMPONENT ~setup-bp-bgt-worldmap.tp2~ ~0~ @900002 //EET should be installed BEFORE BP-BGT Worldmap

/////                                                  \\\\\
///// Common                                           \\\\\
/////                                                  \\\\\

<<<<<<<< .../blank.txt
>>>>>>>>

COPY ~weidu.conf~ ~weidu.conf~
	READ_2DA_ENTRY 0 2 3 "LANGUAGE_BG2"
	INNER_PATCH_SAVE LANGUAGE_BG2 ~%LANGUAGE_BG2%~ BEGIN
		REPLACE_EVALUATE CASE_INSENSITIVE ~\(..\)$~ BEGIN
			TO_UPPER MATCH1
		END ~%MATCH1%~
	END
	PATCH_PRINT ~LANGUAGE_BG2 = %LANGUAGE_BG2%~
BUT_ONLY

ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
	OUTER_SPRINT exe ~.exe~
	OUTER_SPRINT bin ~~
	OUTER_SPRINT WEIDU_EXECUTABLE ~setup-EET.exe~
END ELSE BEGIN //osx, unix
	OUTER_SPRINT exe ~~
	OUTER_SPRINT bin ~./~
	OUTER_SPRINT WEIDU_EXECUTABLE ~./setup-EET~
END

OUTER_SPRINT quote ~"~
OUTER_SPRINT tilde "~"

MKDIR ~EET/temp~

INCLUDE ~EET/lib/bgee_dir.tph~
INCLUDE ~EET/lib/pcu_dict.tph~

/////                                                  \\\\\
///// Detect mods installed on BG:EE                   \\\\\
/////                                                  \\\\\

ACTION_IF FILE_EXISTS ~%bgee_dir%/WeiDU.log~ BEGIN
	COPY + ~%bgee_dir%/WeiDU.log~ ~EET/temp~
		REPLACE_TEXTUALLY ~^//.*~ ~~ //removes "Recently uninstalled" lines
		COUNT_REGEXP_INSTANCES "^~SOA/SETUP-SOA\.TP2~ #[0-9]+ #1 " num_matches
		PATCH_IF (num_matches > 0) BEGIN
			PATCH_FAIL @900006 //You've installed wrong The Stone of Askavar component. EET is compatible with Default version (option 1 during installation).
		END
		COUNT_REGEXP_INSTANCES "^~DRIZZTSAGA/DRIZZTSAGA\.TP2~ #[0-9]+ #1 " num_matches
		PATCH_IF (num_matches > 0) BEGIN
			PATCH_FAIL @900007 //You've installed wrong Drizzt Saga component. EET is compatible with Default version (option 1 during installation).
		END
		REPLACE_TEXTUALLY ~^.~ ~~
		REPLACE_TEXTUALLY ~^.*[/\\]\(.*.TP2\)~ ~\1~
		REPLACE_TEXTUALLY ~\.TP2.~ ~~
		REPLACE_TEXTUALLY ~SETUP-~ ~~
		COUNT_2DA_ROWS 3 "cntrow"
		PATCH_PRINT ~%cntrow% BG:EE mods detected. Checking compatibility with EET...~
		FOR (cnt = 0; cnt < "%cntrow%"; cnt = cnt + 1) BEGIN
			READ_2DA_ENTRY cnt 0 1 "col1"
			PATCH_IF (FILE_CONTAINS_EVALUATED (~EET/tbl/compatibility.tbl~ ~^%col1% ~)) BEGIN
				PATCH_PRINT ~%col1% BG:EE component detected~
			END ELSE PATCH_IF (~%argv[0]%~ STRING_CONTAINS_REGEXP ~[Mm]~ = 0) BEGIN
				PATCH_WARN ~WARNING: %col1% BG:EE mod is not recognized by EET, but M/m flag is assigned, so installation will continue~
			END ELSE BEGIN
				PATCH_FAIL @900008 //%col1% BG:EE mod is not recognized by EET, so it can't be imported to BG2:EE with EET
			END
		END
END

/////                                                  \\\\\
///// TLK merging                                      \\\\\
/////                                                  \\\\\

ACTION_IF NOT FILE_EXISTS ~lang/%LANGUAGE_BG2%/dialog.bak~ BEGIN
	PRINT ~~
	PRINT ~TLK merging...~

	//create backups
	ACTION_FOR_EACH var IN ~~ F BEGIN
		ACTION_IF FILE_EXISTS ~lang/%LANGUAGE_BG2%/dialog%var%.tlk~ BEGIN
			COPY_LARGE + ~lang/%LANGUAGE_BG2%/dialog%var%.tlk~ ~EET/temp/dialog%var%.bak~
		END
	END

	//generate dummy strings up to 111000
	OUTER_SPRINT log ~~
	OUTER_SET tlk_cnt = NEXT_STRREF - 1
	OUTER_FOR (i=%tlk_cnt% ; i<111000 ; i=i+1) BEGIN
		OUTER_SPRINT log "%log%%LNL%@%i% = ~~"
	END
	<<<<<<<< .../append.tra
	>>>>>>>>
	COPY + ~.../append.tra~ ~EET/temp~
	APPEND_OUTER + ~EET/temp/append.tra~ ~%log%~

	//export BG2:EE TLK to TRA
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --traify-tlk --out "EET/temp/bg2ee.tra"~
	//export BG:EE TLK to TRA
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --game "%bgee_dir%" --use-lang "%LANGUAGE_BG1%" --traify# 200000 --traify-tlk --out "EET/temp/bgee.tra"~
	//merge all TRA files, including pre-generated blank.tra
	ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
		AT_NOW ~copy "EET\temp\bg2ee.tra"+"EET\temp\append.tra"+"EET\base\blank.tra"+"EET\temp\bgee.tra" "EET\temp\result.tra"~ EXACT
	END ELSE BEGIN //osx, unix
		AT_NOW ~cat EET/temp/bg2ee.tra EET/temp/append.tra EET/base/blank.tra EET/temp/bgee.tra > EET/temp/result.tra~ EXACT
	END
	//make final dialog.tlk / dialogF.tlk
	AT_NOW ~%WEIDU_EXECUTABLE% --noautoupdate --no-auto-tp2 --logapp --log "EET/temp/bash.debug" --make-tlk "EET/temp/result.tra"~
END

/////                                                  \\\\\
///// Start proper installation (reload TLK)           \\\\\
/////                                                  \\\\\

COPY + ~%WEIDU_EXECUTABLE%~ ~setup-EET_core%exe%~
	~EET/lib/EET_core.tp2~ ~EET_core.tp2~
ACTION_IF ~%LANGUAGE%~ STR_EQ ~en_US~ BEGIN
	OUTER_SET langVar = 0
END ELSE ACTION_IF ~%LANGUAGE%~ STR_EQ ~pl_PL~ BEGIN
	OUTER_SET langVar = 1
END ELSE ACTION_IF ~%LANGUAGE%~ STR_EQ ~de_DE~ BEGIN
	OUTER_SET langVar = 2
END ELSE ACTION_IF ~%LANGUAGE%~ STR_EQ ~fr_FR~ BEGIN
	OUTER_SET langVar = 3
END ELSE ACTION_IF ~%LANGUAGE%~ STR_EQ ~es_ES~ BEGIN
	OUTER_SET langVar = 4
END
AT_EXIT ~%bin%setup-EET_core%exe% --force-install 0 --language %langVar% --skip-at-view --use-lang "%LANGUAGE_BG2%" --args-list %argv[0]% "%bgee_dir%"~
